#! /usr/bin/env nix-shell
#! nix-shell -i "just --justfile" --quiet
#! nix-shell -p just nh git jq sshpass deploy-rs ragenix watchexec

alias rebuild := switch
alias b := switch
alias s := status
alias c := check
alias h := history
alias p := provision
alias d := deploy
alias u := update

_default: update switch

help:
    @just --list --justfile {{ justfile() }} 

git:
    lazygit

shell:
    zsh

update:
    nix flake update --commit-lock-file

secret file:
    vim ./secrets/secrets.nix
    agenix --rules ./secrets/secrets.nix -e ./secrets/{{file}}.age
    agenix --rules ./secrets/secrets.nix -r

deploy *args:
    nix run . -- {{args}}

provision flake host $SSHPASS: check
    nix flake lock
    -ssh-keygen -R {{host}}
    nix run github:nix-community/nixos-anywhere -- --ssh-option StrictHostKeyChecking=no -L --env-password --flake .#{{flake}} --generate-hardware-config nixos-generate-config  hosts/{{flake}}/hardware-configuration.nix root@{{host}}
    -ssh-keygen -R {{host}}

# Build and switch
switch: check && archive
    just --justfile {{ justfile() }} status
    nh os switch -a -D nixdiff

# Flake check
check:
    nix flake check --log-format bar-with-logs

fix:
    nix-collect-garbage -d
    nix-store --verify --check-contents --repair
    nix store verify --all --log-format bar-with-logs --repair --refresh
    just --justfile {{ justfile() }} switch

# show profile history
history:
    nix profile history --profile /nix/var/nix/profiles/system


# clean nix-store and all old generations
[confirm]
gca:
    nh clean all
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

# clean nix-store
gc:
    nh clean all -k 10 -K 1w
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

push:
    git push

# Git status
status:
    -git diff -U0 $(nixos-version --configuration-revision).. -- ':(exclude)*.lock'
    git status -s --renames

# Archive flake inputs
archive:
    nix flake archive
