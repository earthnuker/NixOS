#! /usr/bin/env nix-shell
#! nix-shell -i "just --justfile" --quiet
#! nix-shell -p just nh git jq sshpass deploy-rs ragenix watchexec

alias rebuild := switch
alias b := switch
alias s := status
alias c := check
alias h := history
alias p := provision
alias d := deploy
alias u := update

_default: update switch

# Show help
help:
    @just --list --justfile {{ justfile() }} 

# Start lazygit
git:
    lazygit

# Spawn shell with tools
shell:
    zsh

# Update flake
update:
    nix flake update --commit-lock-file

# Rekey secrets
rekey:
    agenix --rules ./outputs/secrets/secrets.nix -r

# Edit secrets
secret file: && rekey
    vim ./outputs/secrets/secrets.nix
    agenix --rules ./outputs/secrets/secrets.nix -e ./outputs/secrets/{{file}}.age

# Deploy configuration to defined hosts
deploy *args:
    nix run . -- {{args}}

# Provision a host
provision flake host="nixos-installer.lan" $SSHPASS="toor": check
    nix flake lock
    -ssh-keygen -R {{host}}
    nix run github:nix-community/nixos-anywhere -- --ssh-option StrictHostKeyChecking=no -L --env-password --flake .#{{flake}} --generate-hardware-config nixos-generate-config  outputs/hosts/{{flake}}/hardware-configuration.nix root@{{host}}
    -ssh-keygen -R {{host}}

# Build and switch
switch: check && archive
    just --justfile {{ justfile() }} status
    nh os switch -a -D nixdiff

# Flake check
check:
    nix flake check --log-format bar-with-logs

# Archive flake inputs
fix: && switch
    nix-collect-garbage -d
    nix-store --verify --check-contents --repair
    nix store verify --all --log-format bar-with-logs --repair --refresh

# Show profile history
history:
    nix profile history --profile /nix/var/nix/profiles/system


# Clean nix-store and all old generations
[confirm]
gca:
    nh clean all
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

# Clean nix-store and old generations (keep 10 or 1 week)
gc:
    nh clean all -k 10 -K 1w
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

# Git status
status:
    -git diff -U0 $(nixos-version --configuration-revision).. -- ':(exclude)*.lock'
    git status -s --renames

# Archive flake inputs
archive:
    nix flake archive
