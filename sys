#! /usr/bin/env nix-shell
#! nix-shell -i "just --justfile" --quiet
#! nix-shell -p just nh git jq

alias rebuild := switch
alias b := switch
alias s := status
alias c := check
alias h := history
alias p := push

_default:
    @just --list --justfile {{ justfile() }} 

# Build and switch
switch: && archive 
    nix flake update --commit-lock-file
    just --justfile {{ justfile() }} status
    nh os switch -a -D nixdiff 

# Flake check
check:
    nix flake check --log-format multiline-with-logs

# show profile history
history:
    nix profile history --profile /nix/var/nix/profiles/system

# clean nix-store
gc:
    nh clean all -k 10 -K 1w
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

push:
    git push

# Git status
status:
    git diff -U0 $(nixos-version --configuration-revision).. -- ':(exclude)*.lock'
    #git diff -U0 @{push} -- ':(exclude)*.lock'
    git status -s --renames

# Archive flake inputs
archive:
    nix flake archive
