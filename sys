#! /usr/bin/env nix-shell
#! nix-shell -i "just --justfile" --quiet
#! nix-shell -p just nh git jq colmena sshpass deploy-rs

alias rebuild := switch
alias b := switch
alias s := status
alias c := check
alias h := history
alias p := push
alias u := update

_default:
    @just --list --justfile {{ justfile() }} 

git:
    lazygit

shell:
    zsh


update:
    nix flake update --commit-lock-file

deploy *args:
    nix run . -- {{args}}

bootstrap flake $SSHPASS: check
    sshpass -e ssh-copy-id -o StrictHostKeyChecking=no root@nixos-installer.lan
    nix run github:nix-community/nixos-anywhere -- --ssh-option StrictHostKeyChecking=no -L --flake .#{{flake}} --generate-hardware-config nixos-facter hosts/{{flake}}/facter.json root@nixos-installer.lan

# Build and switch
switch: check && archive
    just --justfile {{ justfile() }} status
    nh os switch -a -D nixdiff

# Flake check
check: update
    nix flake check --log-format bar-with-logs

fix:
    nix-collect-garbage -d
    nix-store --verify --check-contents --repair
    nix store verify --all --log-format bar-with-logs --repair --refresh
    just --justfile {{ justfile() }} switch

# show profile history
history:
    nix profile history --profile /nix/var/nix/profiles/system

# clean nix-store
gc:
    nh clean all -k 10 -K 1w
    nix store optimise
    git reflog expire --expire=now --all
    git gc --prune=now --aggressive

push:
    git push

# Git status
status:
    -git diff -U0 $(nixos-version --configuration-revision).. -- ':(exclude)*.lock'
    git status -s --renames

# Archive flake inputs
archive:
    nix flake archive
